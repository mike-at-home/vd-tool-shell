apply from: "$project.rootDir/gradle/dependencies.gradle"
apply from: "$project.rootDir/gradle/spotless.gradle"

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'


android {
    compileSdkVersion androidVersions.compileSdkVersion

    /**
     * These are disabled for build performance reasons as they do not apply to most modules,
     * expected that certain Android modules will need to enabled viewBinding - for that
     * case add `buildFeatures { viewBinding = true }` to that specific module
     */
    buildFeatures {
        viewBinding = false
        aidl = false
        renderScript = false
        shaders = false
        resValues = false
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled false
            def careProviderAppId = '\"' + "$careProviderApplicationId" + '\"'
            buildConfigField 'String', 'APP_ID', careProviderAppId
        }
        dogfood {
            def careProviderAppId = '\"' + "$careProviderApplicationId" + '\"'
            buildConfigField 'String', 'APP_ID', careProviderAppId
        }
        debug {
            debuggable true
            def careProviderAppId = '\"' + "$careProviderApplicationId" + "$careProviderDebugSuffix" + '\"'
            buildConfigField 'String', 'APP_ID', careProviderAppId
        }
        sauce {
            debuggable true
            def careProviderAppId = '\"' + "$careProviderApplicationId" + "$careProviderSauceSuffix" +'\"'
            buildConfigField 'String', 'APP_ID', careProviderAppId
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    testOptions {
        unitTests.all {
            // Workaround for https://github.com/robolectric/robolectric/issues/4534
            systemProperty 'user.home', System.getenv('HOME')
        }
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$google.coreLibraryDesugaringVersion"

    // include lint projects by default for all androidLibrary modules
    implementation project(":lint:design-systems:raw-resources-lint-packaging")

    testImplementation "junit:junit:$testing.jUnitVersion"
}
